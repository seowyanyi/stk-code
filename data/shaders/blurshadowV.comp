uniform sampler2D source;
uniform layout(r32f) restrict writeonly image2D dest;
uniform vec2 pixel;
uniform float weights[17];

// Gaussian separated blur with radius 6.

layout (local_size_x = 4, local_size_y = 16) in;

shared float local_src[4][16 + 2 * 16];

void main()
{
    int x = int(gl_LocalInvocationID.x), y = int(gl_LocalInvocationID.y);
    ivec2 iuv = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
    vec2 uv_m = (iuv - ivec2(0, 16)) * pixel;
    vec2 uv = iuv * pixel;
    vec2 uv_p = (iuv + ivec2(0, 16)) * pixel;

    local_src[x][y] = texture(source, uv_m).x;
    local_src[x][y + 16] = texture(source, uv).x;
    local_src[x][y + 32] = texture(source, uv_p).x;

    barrier();

    float sum = local_src[x][y + 16] * weights[0];
    for (int i = 1; i < 16; i++) {
        sum += local_src[x][16 + y - i] * weights[i];
        sum += local_src[x][16 + y + i] * weights[i];
    }

    imageStore(dest, iuv, vec4(sum));
}
